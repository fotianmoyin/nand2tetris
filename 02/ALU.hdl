// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   //对每一个选项参数，使用选择器进行最终值的选择
   //zx
   And16(a=x, b=false, out=izx);
   Mux16(a=x, b=izx, sel=zx, out=ozx);
   //nx
   Not16(in=ozx, out=inx);
   Mux16(a=ozx, b=inx, sel=nx, out=onx);
   //zy
   And16(a=y, b=false, out=izy);
   Mux16(a=y, b=izy, sel=zy, out=ozy);
   //ny
   Not16(in=ozy, out=iny);
   Mux16(a=ozy, b=iny, sel=ny, out=ony);
   //f
   Add16(a=onx, b=ony, out=fadd);
   And16(a=onx, b=ony, out=fand);
   Mux16(a=fand, b=fadd, sel=f, out=of);
   //no
   Not16(in=of, out=ino);
   Mux16(a=of, b=ino, sel=no, out=out, out[15]=ng, out[0..7]=os, out[8..15]=oe);
   //zr
   Or8Way(in=os, out=o1);
   Or8Way(in=oe, out=o2);
   Or(a=o1, b=o2, out=o3);
   Not(in=o3, out=zr);
}